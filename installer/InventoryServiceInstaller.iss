; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Inventory Service"
#define MyAppVersion "1.0.0"
#define MyAppPublisher "Zafer Balkan"
#define MyAppURL "https://github.com/zbalkan/InventoryService"
#define ParentKey "Software\zb"
#define AppKey "InventoryService"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{70DED3F6-40F9-471C-8EC7-7EB28349632E}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={commonpf}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputDir=.\bin
OutputBaseFilename="{#MyAppName} Installer"
Compression=lzma
SolidCompression=yes
ArchitecturesInstallIn64BitMode=x64
PrivilegesRequired=admin
WizardStyle=modern
Uninstallable=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "..\src\WindowsServiceProxy\bin\x64\Release\*.dll"; DestDir: "{app}"; Flags: ignoreversion
;recursesubdirs createallsubdirs
Source: "..\src\WindowsServiceProxy\bin\x64\Release\*.exe"; DestDir: "{app}"; Flags: ignoreversion
;recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKLM; Subkey: {#ParentKey}; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: {#ParentKey}\{#AppKey}; Flags: uninsdeletekey
Root: HKLM; Subkey: {#ParentKey}\{#AppKey}; ValueType: dword; ValueName: "QueryPeriodInMinutes"; ValueData: 10

[Run]
; Register WMI class
Filename: {dotnet40}\InstallUtil.exe; Parameters: """{app}\InventoryWmiProvider.dll /LogFile=""" ; Flags: runhidden runascurrentuser;
; Register and start service
Filename: {sys}\sc.exe; Parameters: "create ""{#MyAppName}"" start= auto binPath= ""{app}\WindowsServiceProxy.exe""" ; Flags: runhidden runascurrentuser;
Filename: {sys}\sc.exe; Parameters: "description ""{#MyAppName}"" ""Creates software inventory and publishes as WMI object instances.""" ; Flags: runhidden runascurrentuser;
Filename: {sys}\sc.exe; Parameters: "start ""{#MyAppName}""" ; Flags: runhidden runascurrentuser;

[UninstallRun]
; Srop and delete service
Filename: {sys}\sc.exe; Parameters: "stop ""{#MyAppName}""" ; Flags: runhidden runascurrentuser; RunOnceId: "StopService";
Filename: {sys}\sc.exe; Parameters: "delete ""{#MyAppName}""" ; Flags: runhidden runascurrentuser; RunOnceId: "DeleteService";
; Unregister WMI class
Filename: {dotnet40}\InstallUtil.exe; Parameters: "/u {app}\InventoryWmiProvider.dll /LogFile=" ; Flags: runhidden runascurrentuser; RunOnceId: "DelWmiNamespace";
